import org.jetbrains.grammarkit.tasks.GenerateLexerTask

plugins {
    id 'java'
    id 'org.jetbrains.intellij.platform' version '2.6.0'
    id 'org.jetbrains.grammarkit' version '2022.3.2.2'
}

group 'meetzli'
version '2.0'

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

repositories {
    mavenCentral()
    intellijPlatform {
        defaultRepositories()
    }
}

dependencies {
    testImplementation 'junit:junit:4.13.2'

    intellijPlatform {
        intellijIdeaCommunity('2025.1')
        instrumentationTools()
    }
}

intellijPlatform {
    buildSearchableOptions = false

    pluginConfiguration {
        name = 'Cap\'n Proto Support'
        changeNotes = """
          <h1>Version 2.0 - Updated Fork</h1>
          <ul>
            <li>Updated for IntelliJ Platform 2025.1 and RustRover</li>
            <li>Support for all IntelliJ-based IDEs</li>
            <li>Bug fixes and compatibility improvements</li>
            <li>Based on original work by Ahmed T. Youssef</li>
          </ul>
        """
    }

    pluginVerification {
        ides {
            recommended()
        }
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

task cleanLexer(type: Delete) {
    delete file('src/main/java/com/sercapnp/lang/CapnpLexer.java')
}

tasks.named('generateLexer', GenerateLexerTask).configure { task ->
    task.sourceFile.set(layout.projectDirectory.file('src/main/java/com/sercapnp/lang/Capnp.flex'))
    task.targetOutputDir.set(layout.buildDirectory.dir('generated-src/com/sercapnp/lang'))
    task.targetClass.set('CapnpLexer')
    task.purgeOldFiles.set(true)
}

sourceSets.main.java.srcDir layout.buildDirectory.dir('generated-src')

tasks.named('generateLexer').configure { it.dependsOn cleanLexer }
tasks.named('compileJava').configure { it.dependsOn tasks.named('generateLexer') }
